** one-shot trigger (tran): benchmarking implementation of bsim4.8.0 
.param vvd=1.8
md1 4 in vdd vdd  p1 w=3.6u l=0.2u rgeomod=1
md2 4 in 0 0 n1 w=1.8u l=0.2u rgeomod=1
c4 4 0 100f
md3 a 4 vdd vdd  p1 w=3.6u l=0.2u rgeomod=1 
md4 a 4 0 0 n1 w=1.8u l=0.2u rgeomod=1
ca a 0 100f

m1 anot a vdd vdd  p1 w=3.6u l=0.2u rgeomod=1
m2 anot a 0 0 n1 w=1.8u l=0.2u rgeomod=1

m3 bnot in vdd vdd  p1 w=3.6u l=0.2u rgeomod=1
m4 bnot in 0 0 n1 w=1.8u l=0.2u rgeomod=1

m5 aorbnot 0 vdd vdd p1 w=1.8u l=3.6u rgeomod=1
m6 aorbnot in 1 0 n1 w=1.8u l=0.2u rgeomod=1
m7 1 anot 0 0 n1 w=1.8u l=0.2u

m8 lnot 0 vdd vdd p1 w=1.8u l=3.6u rgeomod=1
m9 lnot bnot 2 0 n1 w=1.8u l=0.2u rgeomod=1
m10 2 a 0 0 n1 w=1.8u l=0.2u

m11 out 0 vdd vdd p1 w=3.6u l=3.6u rgeomod=1
m12 out aorbnot 3 0 n1 w=1.8u l=0.2u rgeomod=1
m13 3 lnot 0 0 n1 w=1.8u l=0.2u

vcc vdd 0 1.8
vin in 0 pulse 0 1.8 1ns .1ns .1ns .8ns 5ns

.include modelcard.nmos
.include modelcard.pmos

.tran 1ns 10ns
.print tran in out
.option delmax=5p accurate=1
.meas tran period3 trig v(out) val='0.5*vvd' rise=1  targ v(out) val='0.5*vvd' fall=1
.meas tran period4 trig v(anot) val='0.5*vvd' fall=1  targ v(anot) val='0.5*vvd' rise=1
.meas tran period1 trig v(bnot) val='0.5*vvd' fall=1  targ v(bnot) val='0.5*vvd' rise=1
.meas tran period5 trig v(lnot) val='0.5*vvd' fall=1  targ v(lnot) val='0.5*vvd' fall=2
.meas tran falltime4 trig v(anot) val='0.9*vvd' fall=2  targ v(anot) val='0.1*vvd' fall=2
.meas tran falltime1 trig v(bnot) val='0.9*vvd' fall=2  targ v(bnot) val='0.1*vvd' fall=2
.meas tran falltime5 trig v(lnot) val='0.7*vvd' fall=2  targ v(lnot) val='0.1*vvd' fall=2
.inc modflags.inc
.option post=2 nomod numdgt=7 ingold=2
.end
